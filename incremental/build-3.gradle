import java.io.File;

apply plugin: 'java'

version = '1.0'

sourceCompatibility = '1.6'

repositories {
    mavenCentral()
}

dependencies {
    compile 'commons-collections:commons-collections:3.2.1'
    testCompile group: 'junit', name: 'junit', version: '4.7'
}

//Separate the declarative from the imperative - Mantras of Gradle
task generateMountainFiles(type: ConvertMountainXml) {
    outputDir = "$buildDir/mountains" as File
    xmlFile = file("src/mountains.xml")
    // Try this and see: Should throw an exception
    //xmlFile = file("src/mountainsssss.xml")
    separator = ":"
}

class ConvertMountainXml extends DefaultTask {
    @InputFile
    File xmlFile

    @Input
    String separator

    @OutputDirectory
    File outputDir

    @TaskAction
    public void generate() {
        def mountains = new XmlSlurper().parse(xmlFile)
        mountains.mountain.each { mountain ->
            def mountainFile = new File(outputDir, mountain.name.text() + ".txt")
            mountainFile.text = "feet$separator${mountain.height.text()}"
        }
    }
}
